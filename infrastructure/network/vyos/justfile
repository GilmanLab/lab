set shell := ["bash", "-euo", "pipefail", "-c"]

SQUASHFS := "build/live/filesystem.squashfs"
ROOTFS := "rootfs.tar"
IMAGE := "vyos-gateway:test"
TOPO := "tests/topology.clab.yml"
KEY := "tests/.vyos-test-key"

key:
    test -f "{{KEY}}" || ssh-keygen -t ed25519 -f "{{KEY}}" -N "" -C "vyos-ci"

config: key
    tests/render-config-boot.sh "$(cat {{KEY}}.pub)"

rootfs:
    test -f "{{SQUASHFS}}"
    sqfs2tar "{{SQUASHFS}}" > "{{ROOTFS}}"

image: rootfs
    docker build -t "{{IMAGE}}" -f Dockerfile.containerlab .

deploy: config
    sudo containerlab deploy -t "{{TOPO}}"

destroy:
    sudo containerlab destroy -t "{{TOPO}}" --cleanup

pytest:
    pytest -v tests

test:
    just deploy
    just pytest

clean:
    rm -f "{{ROOTFS}}"
